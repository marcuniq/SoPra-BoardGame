buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE")
        classpath("org.springframework:springloaded:1.2.0.RELEASE")
    }
}


apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'

uploadArchives.dependsOn bootRepackage


idea {
    module {
        inheritOutputDirs = false
        outputDir = file("$buildDir/classes/main/")
    }
}


jar {
    baseName = 'group-09-service'
    version =  '0.0.1-SNAPSHOT'
}

repositories {
    mavenCentral()
    jcenter()
}
configurations {
    newrelic	// Custom configuration to allow for the copying of the New Relic agent at run time.
    springLoaded
}

dependencies {
    newrelic("com.newrelic.agent.java:newrelic-agent:3.14.0")

    springLoaded("org.springframework:springloaded:1.2.0.RELEASE")

    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.hibernate:hibernate-validator")
    compile("org.eclipse.persistence:eclipselink:2.5.2")
    compile("com.h2database:h2:1.4.185")
    compile("org.apache.tomcat:tomcat-servlet-api:8.0.20")
    compile("com.pusher:pusher-rest-java:0.9.0")
    testCompile("junit:junit:4.12")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("org.hamcrest:hamcrest-all:1.3")
    testCompile "org.mockito:mockito-core:1.+"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

task stage { 
  dependsOn "build"
}

bootRun {
    def springLoadedFile = configurations.springLoaded.find {it.name == "springloaded-1.2.0.RELEASE.jar"}

    jvmArgs = ["-javaagent:$springLoadedFile", "-noverify"]
}

task copyAgent(type: Copy) {
    from configurations.newrelic
    into "$buildDir/libs"
    rename ('newrelic-agent-3.14.0.jar', 'newrelic.jar')
    from "$projectDir/newrelic.yml"
    into "$buildDir/libs"
}



springBoot {
    mainClass = 'ch.uzh.ifi.seal.soprafs15.Application'
    agent = file("$buildDir/libs/newrelic.jar")
}

project.tasks.bootRun.dependsOn([copyAgent])


defaultTasks "bootRepackage", "build"
